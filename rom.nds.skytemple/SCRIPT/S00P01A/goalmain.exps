def 0 {
    if ($PERFORMANCE_PROGRESS_LIST[17]) {
        // Dialga Dead
        message_SetFace(ACTOR_ATTENDANT1, FACE_NORMAL, FACE_POS_STANDARD);
        message_Talk(" Our current goal is to defeat\nthat wicked [CS:G]Darkrai[CR] in [CS:P]Dark Crater[CR].[K] To open\nthe way, we need the [CS:C]Legendary Treasures[CR]!");
        message_Talk(" We have [quag:1] Treasures\nright now! Let me see how that stacks up against\nwhat we need to face [CS:G]Darkrai[CR]...");
        switch(ProcessSpecial(102,0, 3)) { // Instruments
            case 0: // Have all of them!
                switch(dungeon_mode(67)) {
                    case DMODE_OPEN_AND_REQUEST:
                        message_SetFace(ACTOR_ATTENDANT1, FACE_JOYOUS, FACE_POS_STANDARD);
                        message_Talk(" GG [hero]! We did it!");
                        break;
                    case DMODE_OPEN:            
                        message_SetFace(ACTOR_ATTENDANT1, FACE_HAPPY, FACE_POS_STANDARD);
                        message_Talk(" If my records are correct, we\nhave all of the treasures we need![K] Let\'s go to\n[CS:P]Dark Crater[CR] and beat [CS:G]Darkrai[CR], [hero]!");
                        break;
                    default:
                        message_SetFace(ACTOR_ATTENDANT1, FACE_WORRIED, FACE_POS_STANDARD);
                        message_Talk(" According to my notes, we have\nall the [CS:I]Instruments[CR] we need![K] But [CS:P]Dark Crater[CR]\nis still closed?[K] Probably [CS:G]Cryptic[CR]\'s fault!");
                        message_Talk(" Okay, so I can open [CS:P]Dark Crater[CR]\nfor us, but [CS:B]definitely report this as a bug[CR] if\nyou see this message, okay [hero]?");
                        screen_FadeOut(1,30);
                        message_Mail("[CN][partner] pulled out a [CS:I]Rusty Trumpet[CR] and\n[CN]began playing it![K] Suddenly, a beam of light\n[CN]burst out of it, heading to the North-West![W:90]");
                        dungeon_mode(67) = DMODE_OPEN;
                        screen_FadeIn(1,30);
                        message_Talk(" There we go, [CS:G]Cresselia[CR]\nshould be ready for us at [CS:P]Dark Crater[CR]![K]\nDon\'t forget, [CS:B]this wasn\'t supposed to happen![CR]");
                        break;
                }
                break;
            
            case 1: // Missing one!
                message_SetFace(ACTOR_ATTENDANT1, FACE_NORMAL, FACE_POS_STANDARD);
                message_Talk(" According to my records, we\'re\njust missing one treasure![K] We need to find\nthat last one before we can go to [CS:P]Dark Crater[CR]!");
                break;
            default: // Missing several!
                message_SetFace(ACTOR_ATTENDANT1, FACE_NORMAL, FACE_POS_STANDARD);
                message_Talk(" According to my records, we\'re\nstill missing several treasures![K] We need to\nfind them all before we can go to [CS:P]Dark Crater[CR]!");
                break;
        }
    } else {
        // Dialga Alive!
        message_SetFace(ACTOR_ATTENDANT1, FACE_NORMAL, FACE_POS_STANDARD);
        message_Talk(" Our current goal is to defeat\n[CS:G]Dialga[CR] at the pinnacle of [CS:P]Temporal Tower[CR]! To\nget there, we need access to [CS:P]Hidden Land[CR]!");
        message_Talk(" [CS:P]Hidden Land[CR] is only accessible\nfrom [CS:G]Lapras[CR] on the beach. In order to summon\nhim, we need enough [CS:I]Relic Fragment[CR] shards!");
        message_Talk(" We have [quag:0] shards\nright now! Let me see how that stacks up against\nwhat we need to find [CS:P]Hidden Land[CR]...");
        switch(dungeon_mode(38)) { // Hidden Land access?
            case DMODE_OPEN_AND_REQUEST:
                // Cleared Hidden Land!
                message_SetFace(ACTOR_ATTENDANT1, FACE_NORMAL, FACE_POS_STANDARD);
                message_Talk(" According to my records,\nwe\'ve already cleared [CS:P]Hidden Land[CR], and\nexplored it top-to-bottom, [hero]!");
                break;
            case DMODE_OPEN:
                // Unlocked Hidden Land!
                message_SetFace(ACTOR_ATTENDANT1, FACE_HAPPY, FACE_POS_STANDARD);
                message_Talk(" According to my records,\n[CS:G]Lapras[CR] is waiting for us on the beach\nto take us to [CS:P]Hidden Land[CR], [hero]!");
                break;
            default: // Hidden Land not Unlocked!
                switch(ProcessSpecial(102,0, 2)) { // Fragment Shards
                    case 0: // Have all of them! ... but this is impossible!
                        message_SetFace(ACTOR_ATTENDANT1, FACE_WORRIED, FACE_POS_STANDARD);
                        message_Talk(" According to my notes, we have\nall the [CS:I]Relic Fragment Shards[CR] we need![K] But\n[CS:P]Hidden Land[CR] is closed?[K] Probably [CS:G]Cryptic[CR]\'s fault!");
                        message_Talk(" Okay, so I can open [CS:P]Hidden Land[CR]\nfor us, but [CS:B]definitely report this as a bug[CR] if\nyou see this message, okay [hero]?");
                        screen_FadeOut(1,30);
                        message_Mail("[CN][partner] held their [CS:I]Relic Fragment[CR] high in\n[CN]the air![K] Suddenly, it began to glow, and a beam\n[CN]of light burst out of it, heading for the beach![W:90]");
                        dungeon_mode(38) = DMODE_OPEN;
                        screen_FadeIn(1,30);
                        message_Talk(" There we go, [CS:G]Lapras[CR] should be\nwaiting for us on the beach right now![K] Don\'t\nforget, [CS:B]this wasn\'t supposed to happen![CR]");
                        break;
                    
                    case 1: // Missing one!
                        message_SetFace(ACTOR_ATTENDANT1, FACE_NORMAL, FACE_POS_STANDARD);
                        message_Talk(" According to my notes, we are\nmissing just one [CS:I]Relic Fragment Shard[CR].[K] I hope\nit isn\'t too hard to find for you, [hero]!");
                        break;

                    default: // Missing several!
                        message_SetFace(ACTOR_ATTENDANT1, FACE_NORMAL, FACE_POS_STANDARD);
                        message_Talk(" According to my notes, we are\nmissing a few [CS:I]Relic Fragment Shards[CR].[K] We need\nthem all in order to reach [CS:P]Hidden Land[CR], okay?");
                        break;      
                }
                break;
        }
        message_SetFace(ACTOR_ATTENDANT1, FACE_NORMAL, FACE_POS_STANDARD);
        message_Talk(" Oh right, I almost forgot![K]\nwe also need to have [CS:P]Temporal Tower[CR] itself\nunlocked to be able to make it there!");
        switch(dungeon_mode(41)) { // Temporal Tower access?
            case DMODE_OPEN_AND_REQUEST:
                message_SetFace(ACTOR_ATTENDANT1, FACE_WORRIED, FACE_POS_STANDARD);
                message_Talk(" According to my notes, we\'ve\nmanaged to clear [CS:P]Temporal Tower[CR] without\nbeating [CS:G]Dialga[CR]... [CS:B]this is definitely a bug![CR]");
                break;                    
            case DMODE_OPEN:
                message_SetFace(ACTOR_ATTENDANT1, FACE_HAPPY, FACE_POS_STANDARD);
                message_Talk(" According to my notes, we\'ve\nmanaged to unlock [CS:P]Temporal Tower[CR], so if we\nmade it to [CS:P]Hidden Land[CR], we could beat [CS:G]Dialga[CR]!");
                break;
            default:
                message_Talk(" According to my notes, we still\nneed to find [CS:P]Temporal Tower[CR] before we can\ndefeat [CS:G]Dialga[CR], even if we clear [CS:P]Hidden Land[CR]!");
                message_Talk(" Keep in mind, it\'s very possible\nthat [CS:P]Temporal Tower[CR] is found in [CS:P]Hidden Land[CR].\n[K]If we can clear [CS:P]Hidden Land[CR], we may want to.");
                message_Talk(" Even if we don\'t find [CS:P]Temporal\nTower[CR] there, [CS:G]Lapras[CR] can take us directly to the\ntower if we have it, and [CS:P]Hidden Land[CR] is clear!");
                break;
        }
        
    }
    end;
}